; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppId "moodbox.com"
#define MyAppName "MoodBox"
#define MyAppVerName "MoodBox 1.4.5 beta"
#define MyAppVersion "1.4.5.0"
#define MyAppSetupName "MoodBoxSetup"
#define MyAppPublisher "Live Communication Tools LLC"
#define MyAppURL "http://moodbox.com/"
#define MyAppExeName "MoodBox.exe"

#define AppDir "..\Release"
#define ClipartDir "..\Clipart"
#define SoundDir "..\Sound"
#define AvatarsDir "..\Avatars"

#define StdFilesFlags "uninsremovereadonly uninsrestartdelete"

; used to delete old stuff
; #define MyOldAppName "Arty Talk"

[Setup]
AppId={#MyAppId}
AppName={#MyAppName}
AppVerName={#MyAppVerName}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
VersionInfoVersion={#MyAppVersion}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
OutputDir=output
OutputBaseFilename={#MyAppSetupName}
UninstallFilesDir={app}\uninstall
Compression=lzma
SolidCompression=yes
DisableProgramGroupPage=yes
DisableReadyPage=yes
SetupIconFile=..\Resources\moodbox.ico

[Languages]
Name: en; MessagesFile: compiler:Default.isl; LicenseFile: eula-en.rtf
; Name: ru; MessagesFile: compiler:Languages\Russian.isl; LicenseFile: license-ru.rtf

[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}
Name: quicklaunchicon; Description: {cm:CreateQuickLaunchIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked

[Files]
Source: {#AppDir}\{#MyAppExeName}; DestDir: {app}; Flags: ignoreversion {#StdFilesFlags}; BeforeInstall: BeforeMyProgInstall('MoodBox.exe')
Source: {#AppDir}\catalogue.pal; DestDir: {app}; Flags: ignoreversion {#StdFilesFlags}
; Source: website.url; DestDir: {app}; Flags: ignoreversion {#StdFilesFlags}
Source: {#ClipartDir}\*; DestDir: {app}\Clipart\; Flags: ignoreversion recursesubdirs createallsubdirs {#StdFilesFlags}
Source: {#SoundDir}\*; DestDir: {app}\Sound\; Flags: ignoreversion {#StdFilesFlags}
Source: {#AvatarsDir}\*; DestDir: {app}\Avatars\; Flags: ignoreversion {#StdFilesFlags}
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: {group}\{#MyAppName}; Filename: {app}\{#MyAppExeName}
; Name: {group}\MoodBox.com; Filename: {app}\website.url
Name: {group}\{cm:ProgramOnTheWeb,{#MyAppName}}; Filename: {#MyAppURL}
Name: {group}\{cm:UninstallProgram,{#MyAppName}}; Filename: {uninstallexe}
Name: {commondesktop}\{#MyAppName}; Filename: {app}\{#MyAppExeName}; Tasks: desktopicon
Name: {userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}; Filename: {app}\{#MyAppExeName}; Tasks: quicklaunchicon

[InstallDelete]
; Type: files; Name: {group}\{#MyOldAppName}.lnk
; Type: files; Name: {group}\{cm:ProgramOnTheWeb,{#MyOldAppName}}.url
; Type: files; Name: {group}\{cm:UninstallProgram,{#MyOldAppName}}.lnk
; Type: files; Name: {commondesktop}\{#MyOldAppName}.lnk
; Type: files; Name: {userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyOldAppName}.lnk

[Run]
Filename: {app}\{#MyAppExeName}; Description: {cm:LaunchProgram,{#MyAppName}}; Flags: nowait postinstall skipifsilent runasoriginaluser

[Registry]
Root: HKCU; Subkey: Software\{#MyAppPublisher}\{#MyAppName}; ValueType: string; ValueName: Current locale; ValueData: {language}; Flags: noerror
[Code]
procedure BeforeMyProgInstall(S: String);
var
ExeFile: String;
begin
  ExeFile := ExpandConstant('{app}') + '\' + S;
  while 1 = 1 do begin
    if FileExists(ExeFile) and not DeleteFile(ExeFile) then begin
      MsgBox('MoodBox.exe is now running and cannot be overwritten.' #13#13 'Exit from MoodBox and press OK', mbInformation, MB_OK);
    end else begin
      break;
        end;
  end;
end;
